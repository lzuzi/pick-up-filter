subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'java-library'

    java.toolchain.languageVersion.set(JavaLanguageVersion.of(21))

    java {
        withSourcesJar()
    }

    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven {
            name = 'FabricMC'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'NeoForged'
            url = 'https://maven.neoforged.net/releases'
        }
        maven {
            name = 'MinecraftForge'
            url = 'https://maven.minecraftforge.net/'
        }
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org/'
        }
        maven {
            name = 'LibrariesMinecraft'
            url = 'https://libraries.minecraft.net/'
        }
    }

    def props = [
            mod_id           : rootProject.findProperty("mod_id") ?: "unknown_id",
            mod_license      : rootProject.findProperty("mod_license") ?: "GPL-3.0-only",
            mod_version      : rootProject.findProperty("mod_version") ?: "1.0.0",
            mod_name         : rootProject.findProperty("mod_name") ?: "Unnamed Mod",
            mod_description  : rootProject.findProperty("mod_description") ?: "No description provided.",
            mod_authors      : rootProject.findProperty("mod_authors") ?: "Anonymous",
            minecraft_version: rootProject.findProperty("minecraft_version") ?: "1.21.5",
            minecraft_version_range: rootProject.findProperty("minecraft_version_range") ?: "1.21.5",
            neo_version_range: rootProject.findProperty("neo_version_range") ?: "1.21.5",
            loader_version_range: rootProject.findProperty("loader_version_range") ?: "[58,)",
            forge_version_range: rootProject.findProperty("forge_version_range") ?: "[58,)"
    ]

    tasks.withType(ProcessResources).configureEach {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE

        inputs.properties(props)
        filteringCharset = "UTF-8"

        ["fabric.mod.json", "META-INF/neoforge.mods.toml", "META-INF/mods.toml", "pack.mcmeta"].each { pattern ->
            filesMatching(pattern) {
                expand props
            }
        }

        from(project(':common').sourceSets.main.resources.srcDirs)
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    /*
    // TODO: this one causes "Duplicate content roots detected" warning.
    // jar property looks fixes this issue and project runs perfectly. gonna keep for future.
    tasks.withType(JavaCompile).configureEach {
        sourceSets.main.java.srcDirs += project(':common').sourceSets.main.java.srcDirs
    }
    jar {
        from {
            duplicatesStrategy = DuplicatesStrategy.EXCLUDE
            project(":common").sourceSets.main.output
        }
    }
     */
}
